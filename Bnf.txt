<programa> ::= <sentenciadeclaracionvar><acciones>|<acciones>

<sentenciadeclaracionvar> ::= DEFVAR <declaracionvar> ENDDEF|DEFVAR <declaracionvar> <declaracionvar> ENDDEF
<declaracionvar> ::= <identificador>:<tipo>;|<declaracionvar>, <identificador>:<tipo>;

<acciones> ::= <accion>|<acciones> <accion>
<accion> ::= <asignacion>|<definicionconstante>|<entrada>|<salida>|<repeat>|<if>

<asignacion> ::= <identificador> = <expresion>;
<definicionconstante> ::= CONST <identificador> = <dato>;

<expresion> ::= <expresion>+<termino>|<expresion>-<termino>|<termino>
<termino> ::= <termino>*<factor>|<termino>/<factor>|<factor>
<factor> ::= <identificador>|(<expresion>)|<cte_string>++<cte_string>|<unaryif>|<qeuqual>|<dato>|<cte_string>

<repeat> ::= REPEAT <acciones> UNTIL <condicion>;
<if> ::= IF <condicion> THEN <acciones> ENDIF|IF <condicion> THEN <acciones> ELSE <acciones> ENDIF
<unaryif> ::= (<condicion> ? <expresion>, <expresion>)
<qequal> ::= QEqual(<expresion>,<lista>)
<entrada> ::= GET <identificador>;
<salida> ::= PUT <expresion>;

<condicion> ::= <comparacion>|<comparacion> AND <comparacion>|<comparacion> OR <comparacion>|!(<comparacion>)
<comparacion> ::= <expresion> < <expresion>|<expresion> > <expresion>|<expresion> <= <expresion>  
                            |<expresion> >= <expresion>|<expresion> <> <expresion>

<comentarios> ::= /!<alfanumerico>!/|/!<alfanumerico>/!<alfanumerico>!/<alfanumerico>!/

<identificador> ::= <letra>|<letra><alfanumerico> 
<alfanumerico> ::= <letra>|<digito>|<alfanumerico><letra>|<alfanumerico><digito>
<cte_entero> ::= <digito>|<cte_entero><digito>
<cte_real> ::= <cte_entero>.<cte_entero>|.<cte_entero>|<cte_entero>.
<cte_string> ::= "<alfanumerico>"
<dato> ::= <cte_entero>|<cte_real>|<cte_string>

<lista> ::=  [<elementoLista>]
<elementoLista> ::= <expresion>|<elementoLista>,<expresion>

<tipo> ::= Int|Real|String
<digito> ::= 0|1|2|3|4|5|6|7|8|9
<letra> ::= 'a'|'b'|'c'.....'x'|'y'|'z'|'A'|'B'|'C'.....'X'|'Y'|'Z'|' '|''
